****************************************************************RACHK*
* COBOL WRAPPER FOR RACROUTE REQUEST=AUTH CALL                       *
* USAGE:                                                             *
*       01  RACHK-COMMAREA.                                          *
*           05 RACHK-RETURNCODE.                                     *
*           05 RACHK-RACFPLEN.                                       *
*           05 RACHK-RACFPROF.                                       *
*                                                                    *
*       move 'Test.Read.On.This.EJBROLE' to RACHK-RACFPROF           *
*       move 25 to RACHK-RACFPLEN                                    *
*       call 'RACHK' using RACHK-COMMAREA                            *
*       IF RACHK-RETURNCODE = 0 THEN                                 *
*       do-stuff-because-user-is-authorized                          *
*       ELSE                                                         *
*       do-other-stuff-because-user-is-not-authorized                *
*                                                                    *
**********************************************************************
RACHK    CSECT
RACHK    AMODE 31
RACHK    RMODE ANY
         J     START
         DC    AL1(25)
         DC    CL8'RACHK'
         DC    CL9'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    CL11'H.B. Kuiper'

START    DS    0H
         BAKR  R14,0
         LR    R12,R15
         USING RACHK,R12

*
**********************************************************************
* Parameter List                                                     *
**********************************************************************
*                                        *****************************
         L     R11,0(0,R1)               * Paramter list             *
         USING RACFPARM,R11              * Address Parameters        *
*                                        *****************************
CHECKIT  EQU   *
         LH    R6,RACFPLEN               * PLEN TO R6
         STH   R6,PROFLL                 * STORE INTO ENTITY N
         BCTR  R6,0                      * R6 = R6 - 1
         EX    R6,EXMVC                  * PROFILENAME INTO ENTITY
*                                        * EXECUTE RACROUTE MACRO
*                                        * WE SUPRESS MESSAGES AS WE
*                                        * DONT WANT ICH408Is
         RACROUTE REQUEST=AUTH,WORKA=WORK,                             *
               ENTITYX=ENTITY,RELEASE=1.9,                             *
               ATTR=READ,CLASS='EJBROLE',MSGSUPP=YES
         ST    R15,RACFRC                * RACROUTE RC = OUR RC
         LA    R15,0                     * MODULE ALWAYS RC=00
         B     RETURN                    * LETS'S END THIS

RETURN   PR                              * POP REGS AND RETURN

EXMVC    MVC  PROFILE(0),RACFPROF

**********************************************************************
* WORKAREA RACROUTE                                                  *
**********************************************************************
WORK      DS    XL512
*         ENTITY 'STRUCT' FOR RACROUTE
ENTITY    DC    H'0'
PROFLL    DC    H'0'
PROFILE   DC    CL255' '

**********************************************************************
* COBOL COMMUNICATION STRUCTURE                                      *
**********************************************************************
RACFPARM  DSECT
RACFRC    DS    F                        * RETURN CODE
RACFBUFF  DS    H                        *
RACFPLEN  DS    H                        * 2 BYTE LEN OF
RACFPROF  DS    CL255                    * PROFILENAME

**********************************************************************
* Equates                                                            *
**********************************************************************
R0        EQU   0
R1        EQU   1
R2        EQU   2
R3        EQU   3
R4        EQU   4
R5        EQU   5
R6        EQU   6
R7        EQU   7
R8        EQU   8
R9        EQU   9
R10       EQU   10
R11       EQU   11
R12       EQU   12
R13       EQU   13
R14       EQU   14
R15       EQU   15
          END RACHK
